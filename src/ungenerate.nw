% -*- mode: Noweb; noweb-code-mode: icon-mode -*-
This program rewrites files that have line numebrs which refer to the
file \texttt{generated-code}, replacing them with references to the
file itself.
<<*>>=
procedure main(args)
  if *args = 0 then {
    write(&errout, "Usage: ungenerate files")
    write(&errout, "  (can't be used as a filter)")
    exit(1)
  }
  every ungenerate(!args)
end
<<*>>=
procedure ungenerate(fname)
  local line, lines, file
  lines := []
  file := open(fname, "r") | stop("Cannot open file ", fname, " for read")
  while line := read(file) do
    line ?
      if (optwhite(), ="#", optwhite(), ="line", white(), tab(many(&digits)),
	  white(), =image("generated-code"), optwhite(), pos(0)) then
        put(lines, "#line " || (*lines + 2) || " " || image(fname))
      else
        put(lines, line)
  close(file)
  file := open(fname, "w") | stop("Cannot open file ", fname, " for write")
  every write(file, !lines)
  close(file)
  return
end
<<*>>=
procedure white()
  suspend tab(many(' \t'))
end

procedure optwhite()
  suspend white() | ""
end
